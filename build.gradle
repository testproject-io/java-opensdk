/*
 * Copyright 2020 TestProject LTD. and/or its affiliates
 * and other contributors as indicated by the @author tags.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'java-library'
    id 'checkstyle'
    id 'com.github.spotbugs' version "4.2.3"
    id 'maven-publish'
    id 'signing'
    id 'io.codearte.nexus-staging' version '0.21.2'
    id 'com.adarshr.test-logger' version '2.1.0'
}

println("> Using Java: " + JavaVersion.current())
if (JavaVersion.current() < JavaVersion.VERSION_11) {
    throw new GradleException("Build requires JDK 11 or higher.")
}

sourceCompatibility = 11
targetCompatibility = 11

def sdkVersion = !project.version.equals("unspecified") ? project.version : "0.0.1"
def variant = project.hasProperty("release") && project.getProperty("release") ? "RELEASE" : "SNAPSHOT"

group 'io.testproject'
version "${sdkVersion}-${variant}"
println("> Building ${version}")

repositories {
    jcenter()
    maven { url 'https://jitpack.io' }
}

dependencies {

    // Java client for Appium Mobile Webdriver
    api ('com.github.appium:java-client:07483273030fe97045caa89c01279f9bf3842dbb') {
        exclude group: 'com.google.guava', module: 'guava'
    }

    // Guava
    api group: 'com.google.guava', name: 'guava', version: '29.0-jre'

    // JUnit4
    implementation group: 'junit', name: 'junit', version: '4.13'

    // JUnit5
    implementation 'org.junit.jupiter:junit-jupiter-api:5.5.1'

    implementation 'org.junit.jupiter:junit-jupiter-params:5.5.1'

    // Module "junit-jupiter-params" of JUnit 5.
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.5.1'

    // TestNG Testing framework
    implementation group: 'org.testng', name: 'testng', version: '7.1.0'

    // Cucumber framework.
    implementation 'io.cucumber:cucumber-java:6.9.1'

    testImplementation group: 'io.cucumber', name: 'cucumber-junit', version: '6.9.1'

    // Logback classic module
    testImplementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'

    // Logback core module
    testImplementation group: 'ch.qos.logback', name: 'logback-core', version: '1.2.3'

    // Annotations the SpotBugs tool supports
    implementation group: 'com.github.spotbugs', name: 'spotbugs-annotations', version: '4.0.2'

    // Use JUnit Jupiter Engine for testing.
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.5.1'

    // Maven Artifact
    implementation 'org.apache.maven:maven-artifact:3.6.3'

    // Apache commons
    implementation group: 'org.apache.commons', name: 'commons-csv', version: '1.8'

    // Java API for RESTful Web Services
    implementation 'javax.ws.rs:javax.ws.rs-api:2.1.1'

    api 'org.apache.httpcomponents:httpclient:4.5.13'
    api 'com.google.code.gson:gson:2.8.9'
    api 'org.apache.commons:commons-lang3:3.12.0'
    api 'org.seleniumhq.selenium:selenium-java:4.0.0'
    api "commons-io:commons-io:2.6"
}

checkstyle.configFile file("config/checkstyle/checkstyle.xml")
checkstyleMain.source = 'src/main/java'
checkstyleTest.source = 'src/test/java'

spotbugs {
    ignoreFailures = false
    showProgress = true
    effort = 'max'
}
spotbugsMain {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}
spotbugsTest {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    options.addStringOption('encoding', 'UTF-8')
}


task generateVersionInfo {
    def genOutputDir = file("src/main/resources")
    def outputFile = file("$genOutputDir/testproject-opensdk.properties")
    outputs.file(outputFile)
    doLast {
        outputFile.text = "version=${version}"
    }
}

jar {
    dependsOn generateVersionInfo
    manifest {
        attributes 'Implementation-Title': project.name,
                 'Implementation-Version': project.version
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'io.testproject'
            artifactId = 'java-sdk'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'TestProject SDK'
                description = 'TestProject SDK for Java'
                url = 'https://testproject.io'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'mstrelex'
                        name = 'Marat Strelets'
                        email = 'marat@testproject.io'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/testproject-io/java-opensdk.git'
                    developerConnection = 'scm:git:ssh://github.com/testproject-io/java-opensdk.git'
                    url = 'https://github.com/testproject-io/java-opensdk'
                }
            }
        }
    }

    repositories {
        maven {

            def ossrhUsername = findProperty("ossrhUsername")
            def ossrhPassword = findProperty("ossrhPassword")

            credentials {
                username "$ossrhUsername"
                password "$ossrhPassword"
            }
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

signing {
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}

nexusStaging {
    def ossrhUsername = findProperty("ossrhUsername")
    def ossrhPassword = findProperty("ossrhPassword")

    username "$ossrhUsername"
    password "$ossrhPassword"

    numberOfRetries 30
    delayBetweenRetriesInMillis 10000
}

wrapper {
    gradleVersion = '6.4.1'
    distributionType = Wrapper.DistributionType.ALL
}

test {
    if (project.hasProperty('framework')) {
        String framework = getProperty('framework')
        if (framework == "JUNIT4") {
            useJUnit()
            println("> Using JUnit 4 for testing...")
        } else if (framework == "JUNIT5") {
            useJUnitPlatform()
            println("> Using JUnit 5 for testing...")
        } else if (framework == "TESTNG") {
            useTestNG()
            println("> Using TestNG for testing...")
        } else {
            println("> Unsupported framework specified: " + framework)
        }
    } else {
        println("> Using default framework: JUnit 5")
        useJUnitPlatform()
    }
}

// Expose argument names in tests
compileTestJava.options.compilerArgs.add '-parameters'
